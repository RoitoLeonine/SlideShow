import java.time.Instant
import java.time.format.DateTimeFormatter
import java.time.temporal.ChronoUnit

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = '0.3.0-1.16.2'
group = 'org.teaconmc'
archivesBaseName = 'SlideShow'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: 'snapshot', version: '20200820-1.16.1'
    
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'info'
            mods {
                slide_show {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run_server')
            property 'forge.logging.console.level', 'info'
            mods {
                slide_show {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'info'
            args '--mod', 'slide_show', '--all', '--output', file('src/generated/resources/')
            mods {
                slide_show {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        url 'https://www.dogforce-games.com/maven/'
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.2-33.0.37'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "Slide-Show",
            "Specification-Vendor": "TeaConMC",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor": "TeaConMC",
            "Implementation-Timestamp": DateTimeFormatter.ISO_INSTANT.format(Instant.now().truncatedTo(ChronoUnit.SECONDS))
        ])
    }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        release(MavenPublication) {
            groupId = "org.teacon"
            artifactId = "SlideShow-Forge-1.16.2"

            artifact reobfArtifact
            pom {
                name = 'SlideShow for Minecraft 1.16.2'
                description = 'Minecraft mod, adding a projector that can display online images.'
                url = 'https://github.com/RoitoLeonine/SlideShow'
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://github.com/RoitoLeonine/SlideShow/blob/1.16-forge/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = '3TUSK'
                        name = '3TUSK'
                    }
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/RoitoLeonine/SlideShow/issues'
                }
                scm {
                    url = 'https://github.com/RoitoLeonine/SlideShow'
                    connection = 'scm:git:git://github.com/RoitoLeonine/SlideShow.git'
                    developerConnection = 'scm:git:git@github.com:RoitoLeonine/SlideShow.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = "archive"
            url = "http://maven.lemonslice.cloud/releases"
            credentials {
                username = System.env.MAVEN_USERNAME
                password = System.env.MAVEN_PASSWORD
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    onlyIf {
        System.env.MAVEN_USERNAME && System.env.MAVEN_PASSWORD
    }
}

/**
 * A simple task to pass down the artifact name and path to other GitHub actions.
 */
task("githubActionOutput") {
    onlyIf {
        System.env.GITHUB_ACTIONS
    }
    doLast {
        println "::set-output name=artifact_path::$buildDir/reobfJar/output.jar"
        println "::set-output name=artifact_name::$archivesBaseName-${version}.jar"
    }
}